const std = @import("std");
const Io = std.Io;
const Scanner = std.json.Scanner;
const Token = std.json.Token;

const stats = @import("distributions");
const ContDist = stats.ContinuousDistribution;
const Interval = stats.Interval;

const Precision = @import("../config.zig").Precision;

const ParseError = @import("parse.zig").ParseError;
const JsonScannerError = @import("parse.zig").JsonScannerError;
const readKeyNumber = @import("parse.zig").readKeyNumber;
const readKeyBool = @import("parse.zig").readKeyBool;

pub fn parseExponential(scanner: *Scanner, stderr: *Io.Writer) (ParseError || JsonScannerError || error{ InvalidCharacter, WriteFailed })!ContDist(Precision) {
    if (try scanner.next() != Token.object_begin) return error.UnexpectedToken;

    var mean: ?Precision = null;
    var rate: ?Precision = null;

    while (true) {
        const tok = try scanner.next();
        if (tok == Token.object_end) break;
        if (tok != Token.string) return error.UnexpectedToken;

        const num = try readKeyNumber(scanner, Precision);
        if (std.mem.eql(u8, tok.string, "mean")) {
            mean = num;
        } else if (std.mem.eql(u8, tok.string, "rate")) {
            rate = num;
        } else {
            try stderr.print("exponential: unknown param '{s}'\n", .{tok.string});
            return error.UnknownParameter;
        }
    }

    if (mean) |m| return ContDist(Precision){ .exponential = stats.Exponential(Precision).initMean(m) };
    if (rate) |r| return ContDist(Precision){ .exponential = stats.Exponential(Precision).init(r) };
    try stderr.print("exponential: missing required param 'mean' or 'rate'\n", .{});
    return error.MissingField;
}

pub fn parsePareto(scanner: *Scanner, stderr: *Io.Writer) (ParseError || JsonScannerError || error{ InvalidCharacter, WriteFailed })!ContDist(Precision) {
    if (try scanner.next() != Token.object_begin) return error.UnexpectedToken;

    var shape: ?Precision = null;
    var scale: ?Precision = null;

    while (true) {
        const tok = try scanner.next();
        if (tok == Token.object_end) break;
        if (tok != Token.string) return error.UnexpectedToken;

        const num = try readKeyNumber(scanner, Precision);
        if (std.mem.eql(u8, tok.string, "shape")) {
            shape = num;
        } else if (std.mem.eql(u8, tok.string, "scale")) {
            scale = num;
        } else {
            try stderr.print("pareto: unknown param '{s}'\n", .{tok.string});
            return error.UnknownParameter;
        }
    }

    if (shape == null or scale == null) {
        try stderr.print("pareto: missing required param (need 'shape' and 'scale')\n", .{});
        return error.MissingField;
    }
    return ContDist(Precision){ .pareto = stats.Pareto(Precision).init(shape.?, scale.?) };
}

pub fn parseUniform(scanner: *Scanner, param_name: []const u8, stderr: *Io.Writer) (ParseError || JsonScannerError || error{ InvalidCharacter, WriteFailed })!ContDist(Precision) {
    if (try scanner.next() != Token.object_begin) return error.UnexpectedToken;

    var min: ?Precision = null;
    var max: ?Precision = null;
    var interval: ?Interval = null;

    while (true) {
        const tok = try scanner.next();
        if (tok == Token.object_end) break;
        if (tok != Token.string) return error.UnexpectedToken;

        if (std.mem.eql(u8, tok.string, "min")) {
            min = try readKeyNumber(scanner, Precision);
            if (min.? < 0) try stderr.print("warning - min ({d}) is negative in '{s}', this could lead to negative times", .{ min.?, param_name });
        } else if (std.mem.eql(u8, tok.string, "max")) {
            max = try readKeyNumber(scanner, Precision);
            if (max.? < 0) try stderr.print("warning - max ({d}, and therefore min too) is negative in '{s}', this could lead to negative times", .{ max.?, param_name });
        } else if (std.mem.eql(u8, tok.string, "interval")) {
            const s = (try scanner.next()).string;
            interval = std.meta.stringToEnum(Interval, s) orelse {
                try stderr.print("uniform: invalid interval '{s}' in '{s}'\n", .{ s, param_name });
                return error.InvalidInterval;
            };
        } else {
            try stderr.print("uniform: unknown param '{s}'\n", .{tok.string});
            return error.UnknownParameter;
        }
    }

    if (min == null or max == null or interval == null) {
        try stderr.print("uniform: missing required param (need 'min', 'max' and 'interval')\n", .{});
        return error.MissingField;
    }

    // we know here that they are definetly not null
    if (min.? > max.?) {
        try stderr.print("warning- min is bigger than max in '{s}'", .{param_name});
        return error.InvalidInterval;
    }
    return ContDist(Precision){ .uniform = stats.Uniform(Precision).init(min.?, max.?, interval.?) };
}

pub fn parseConstant(scanner: *Scanner, param_name: []const u8, stderr: *Io.Writer) (ParseError || JsonScannerError || error{ InvalidCharacter, WriteFailed })!ContDist(Precision) {
    if (try scanner.next() != Token.object_begin) return error.UnexpectedToken;

    var value: ?Precision = null;

    while (true) {
        const tok = try scanner.next();
        if (tok == Token.object_end) break;
        if (tok != Token.string) return error.UnexpectedToken;

        if (std.mem.eql(u8, tok.string, "value")) {
            value = try readKeyNumber(scanner, Precision);
            if (value.? < 0) try stderr.print("warning - value of 'constant' is negative in '{s}'", .{param_name});
        } else {
            try stderr.print("constant: unknown param '{s}' in '{s}'\n", .{ tok.string, param_name });
            return error.UnknownParameter;
        }
    }

    if (value == null) {
        try stderr.print("constant: missing required param 'value'\n", .{});
        return error.MissingField;
    }
    return ContDist(Precision){ .constant = stats.Constant(Precision).init(value.?) };
}

pub fn parseNormal(scanner: *Scanner, stderr: *Io.Writer) (ParseError || JsonScannerError || error{ InvalidCharacter, WriteFailed })!ContDist(Precision) {
    if (try scanner.next() != Token.object_begin) return error.UnexpectedToken;

    var mean: ?Precision = null;
    var variance: ?Precision = null;

    while (true) {
        const tok = try scanner.next();
        if (tok == Token.object_end) break;
        if (tok != Token.string) return error.UnexpectedToken;

        const num = try readKeyNumber(scanner, Precision);
        if (std.mem.eql(u8, tok.string, "mean")) {
            mean = num;
        } else if (std.mem.eql(u8, tok.string, "variance")) {
            variance = num;
        } else {
            try stderr.print("normal: unknown param '{s}'\n", .{tok.string});
            return error.UnknownParameter;
        }
    }

    if (mean == null or variance == null) {
        try stderr.print("normal: missing required param (need 'mean' and 'variance')\n", .{});
        return error.MissingField;
    }
    return ContDist(Precision){ .normal = stats.Normal(Precision).init(mean.?, variance.?) };
}
